buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'groovy'
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC4-3"
    id 'net.saliman.cobertura' version '2.5.0'
}

apply plugin: 'kotlin'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

version '4.0.5'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile 'com.github.thetric:ilias-downloader-connector-api:4.0.3'

    // web parser deps
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.9.0'

    // logging libs
    compile 'io.github.microutils:kotlin-logging:1.4.6'
    compile 'org.slf4j:slf4j-api:1.7.25'

    // test
    testCompile 'org.codehaus.groovy:groovy:2.4.12'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'net.bytebuddy:byte-buddy:1.7.5'
}

apply from: file('gradle/coverage.gradle')
apply from: file('gradle/detekt.gradle')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    // FIXME currently this creates an empty jar
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
