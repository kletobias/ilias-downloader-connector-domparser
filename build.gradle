buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'groovy'
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
    id 'net.saliman.cobertura' version '2.5.0'
    id 'java-library'
}

apply plugin: 'kotlin'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

version '4.0.8'

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    api 'com.github.thetric:ilias-downloader-connector-api:4.0.4'

    // web parser deps
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.9.1'

    // logging libs
    implementation 'io.github.microutils:kotlin-logging:1.5.3'
    runtimeOnly 'org.slf4j:slf4j-api:1.7.25'

    // test
    testImplementation 'org.codehaus.groovy:groovy:2.4.13'
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.7.10'
}

apply from: file('gradle/coverage.gradle')
apply from: file('gradle/detekt.gradle')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    // FIXME currently this creates an empty jar
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
