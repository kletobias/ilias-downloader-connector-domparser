buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'groovy'
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-4"
    id 'net.saliman.cobertura' version '2.5.4'
    id 'java-library'
}

apply plugin: 'kotlin'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

version '4.0.9'

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    api 'com.github.thetric:ilias-downloader-connector-api:5.0.0'

    // web parser deps
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'

    // logging libs
    implementation 'io.github.microutils:kotlin-logging:1.5.4'
    runtimeOnly 'org.slf4j:slf4j-api:1.7.25'

    // test
    testImplementation 'org.codehaus.groovy:groovy:2.4.15'
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.8.3'
}

apply from: file('gradle/coverage.gradle')
apply from: file('gradle/detekt.gradle')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}
